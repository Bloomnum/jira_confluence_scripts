import com.atlassian.jira.component.ComponentAccessor
import com.onresolve.scriptrunner.runner.customisers.WithPlugin
import com.onresolve.scriptrunner.runner.customisers.PluginModule
import com.tempoplugin.worklog.v4.rest.InputWorklogsFactory
import com.tempoplugin.worklog.v4.rest.TimesheetWorklogBean
import com.tempoplugin.worklog.v4.rest.WorkAttributeValueInputBean
import com.tempoplugin.worklog.v4.services.WorklogService
import com.tempoplugin.core.workattribute.api.WorkAttributeService
import java.text.SimpleDateFormat
import java.util.Calendar
import java.util.concurrent.TimeUnit

@WithPlugin('is.origo.jira.tempo-plugin')
@PluginModule WorklogService worklogService
@PluginModule InputWorklogsFactory inputWorklogsFactory
@PluginModule WorkAttributeService workAttributeService

def issueManager = ComponentAccessor.issueManager
def cfManager = ComponentAccessor.customFieldManager

// Конфигурация
def triggerIssue = issueManager.getIssueByCurrentKey(ruleContext.renderSmartValues("{{triggerIssue}}"))
def SOURCE_ISSUE_KEY = triggerIssue.toString()
def TARGET_ISSUE_KEY = 'LIFE-1' // Задача для списывания отпуска
def CF_START_DATE_ID = 13900L
def CF_DAYS_ID = 21204L
def CF_ACCOUNT_ID = 13600L
def HOURS_PER_DAY = 8
def DEFAULT_ACCOUNT = "lo-14"

def sourceIssue = issueManager.getIssueObject(SOURCE_ISSUE_KEY)
def reporter = sourceIssue.reporter

// Получение атрибута аккаунта
def accountAttributeKey = null
try {
    accountAttributeKey = workAttributeService.workAttributes.get().find {
        it.name in ["Аккаунт", "_Account_", "_Проект_", "Account", "Project"]
    }?.key
} catch (e) {}

// Получение значения аккаунта
def accountValue = DEFAULT_ACCOUNT
try {
    def accountField = cfManager.getCustomFieldObject(CF_ACCOUNT_ID)
    def value = sourceIssue.getCustomFieldValue(accountField)
    if (value) accountValue = value.toString()
} catch (e) {}

// Получение дат
def startDate = sourceIssue.getCustomFieldValue(cfManager.getCustomFieldObject(CF_START_DATE_ID)) as Date
def daysRaw = sourceIssue.getCustomFieldValue(cfManager.getCustomFieldObject(CF_DAYS_ID))
def totalDays = daysRaw instanceof Number ? daysRaw.intValue() : 
                (daysRaw?.toString()?.trim()?.isInteger() ? daysRaw.toString().trim().toInteger() : 0)

if (!startDate || totalDays <= 0) {
    addMessage("Ошибка: не заполнены дата начала или количество дней")
}

// Создание worklogs
def cal = Calendar.instance
cal.time = startDate
def iso = new SimpleDateFormat('yyyy-MM-dd')
int daysProcessed = 0
int worklogsCreated = 0

while (daysProcessed < totalDays) {
    if (cal.get(Calendar.DAY_OF_WEEK) !in [Calendar.SATURDAY, Calendar.SUNDAY]) {
        try {
            def builder = new TimesheetWorklogBean.Builder()
                .issueIdOrKey(TARGET_ISSUE_KEY)
                .comment("Отпуск (авто из ${SOURCE_ISSUE_KEY})")
                .startDate(iso.format(cal.time))
                .workerKey(reporter.key)
                .timeSpentSeconds(TimeUnit.HOURS.toSeconds(HOURS_PER_DAY))
            
            if (accountAttributeKey && accountValue) {
                builder.attributes([(accountAttributeKey): new WorkAttributeValueInputBean(accountValue)])
            }
            
            worklogService.createTempoWorklogs(inputWorklogsFactory.buildForCreate(builder.build()))
            worklogsCreated++
        } catch (e) {
            addMessage("Ошибка создания worklog: ${e.message}")
        }
    }
    daysProcessed++
    cal.add(Calendar.DAY_OF_MONTH, 1)
}

//return "Создано ${worklogsCreated} worklogs из ${totalDays} дней отпуска для ${reporter.displayName}"
